// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/02/ALU.hdl

/**
 * The ALU (Arithmetic Logic Unit).
 * Computes one of the following functions:
 * x+y, x-y, y-x, 0, 1, -1, x, y, -x, -y, !x, !y,
 * x+1, y+1, x-1, y-1, x&y, x|y on two 16-bit inputs, 
 * according to 6 input bits denoted zx,nx,zy,ny,f,no.
 * In addition, the ALU computes two 1-bit outputs:
 * if the ALU output == 0, zr is set to 1; otherwise zr is set to 0;
 * if the ALU output < 0, ng is set to 1; otherwise ng is set to 0.
 */

// Implementation: the ALU logic manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) set x = 0        // 16-bit constant
// if (nx == 1) set x = !x       // bitwise not
// if (zy == 1) set y = 0        // 16-bit constant
// if (ny == 1) set y = !y       // bitwise not
// if (f == 1)  set out = x + y  // integer 2's complement addition
// if (f == 0)  set out = x & y  // bitwise and
// if (no == 1) set out = !out   // bitwise not
// if (out == 0) set zr = 1
// if (out < 0) set ng = 1

CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute out = x + y (if 1) or x & y (if 0)
        no; // negate the out output?

    OUT 
        out[16], // 16-bit output
        zr, // 1 if (out == 0), 0 otherwise
        ng; // 1 if (out < 0),  0 otherwise

    PARTS:
	Mux16(a=x, b=false, sel=zx, out=newx);//if necessary x=0.
	Mux16(a=y, b=false, sel=zy, out=newy);//if necessary x=0.
	
	Not16(in=newx, out=negx);//save the value of !x.
	Not16(in=newy, out=negy);//save the value of !y.
	Mux16(a=newx, b=negx, sel=nx, out=finalx);//if necessary x=!x.
	Mux16(a=newy, b=negy, sel=ny, out=finaly);//if necessary y=!y.
	
	And16(a=finalx, b=finaly, out=xandy);//save the value of x&y.
	Add16(a=finalx, b=finaly, out=xplusy);//save the value of x+y.
	Mux16(a=xandy, b=xplusy, sel=f, out=newout);//save choose between x&y and x+y.
	
	Not16(in=newout, out=nego);//save the value of !out.
	Mux16(a=newout, b=nego, sel=no, out=out);//if necessary out=!out.
	
	Mux16(a=newout, b=nego, sel=no, out=tempout);//save the value of out in tempout.
	Or16(a=tempout, b=false, out[15]=ng);//put in ng the value of the msb of tempout.
	
	Mux16(a=tempout, b=false, sel=false, out[0..7]=low, out[8..15]=high);//split out to two buses.
	Or8Way(in=low, out=first);//or8way on right side.
	Or8Way(in=high, out=second);//or8way on left side.
	Or(a=first, b=second, out=negresult);//or between the sides.
	Not(in=negresult, out=zr);//if negresult=0 it means out=0 => zr=0, else zr=1.	
}